# Day 9 Part 1

```elixir
Mix.install([])
```

## Part 1

```elixir
defmodule Day9Part1 do
  def run(input) do
    tail_position = {0, 0}
    head_position = {0, 0}

    input
    |> String.split("\n", trim: true)
    |> execute_moves(head_position, tail_position, MapSet.new([tail_position]))
    |> MapSet.size()
  end

  defp execute_moves(
         [current_move | rest_moves],
         head_position,
         tail_position,
         visited_tail_positions
       ) do
    [move_direction, move_count] = String.split(current_move, " ")
    moves = repeat_value(String.to_integer(move_count), move_direction)

    {head_position, tail_position, visited_tail_positions} =
      Enum.reduce(moves, {head_position, tail_position, visited_tail_positions}, fn move,
                                                                                    {head_position,
                                                                                     tail_position,
                                                                                     visited_tail_positions} ->
        head_position = execute_head_move(head_position, move)
        tail_position = execute_tail_move(head_position, tail_position)

        visited_tail_positions = MapSet.put(visited_tail_positions, tail_position)

        {head_position, tail_position, visited_tail_positions}
      end)

    execute_moves(rest_moves, head_position, tail_position, visited_tail_positions)
  end

  defp execute_moves([], _head_position, _tail_position, visited_tail_positions) do
    visited_tail_positions
  end

  defp execute_head_move(head_position, move_direction) do
    execute_move(head_position, move_direction)
  end

  defp execute_tail_move({head_x, head_y}, {tail_x, tail_y}) do
    real_diff_x = head_x - tail_x
    real_diff_y = head_y - tail_y

    if adjacent?(real_diff_x, real_diff_y) do
      {tail_x, tail_y}
    else
      diff_x = max(min(head_x - tail_x, 1), -1)
      diff_y = max(min(head_y - tail_y, 1), -1)

      {tail_x + diff_x, tail_y + diff_y}
    end
  end

  defp adjacent?(diff_x, diff_y) do
    diff_x <= 1 and diff_y <= 1 and diff_x >= -1 and diff_y >= -1
  end

  defp execute_move({x, y}, "U"), do: {x, y + 1}
  defp execute_move({x, y}, "D"), do: {x, y - 1}
  defp execute_move({x, y}, "L"), do: {x - 1, y}
  defp execute_move({x, y}, "R"), do: {x + 1, y}

  defp repeat_value(number_of_times, value) do
    Enum.map(0..(number_of_times - 1), fn _index -> value end)
  end
end
```

## Tests

```elixir
ExUnit.start(autorun: false)

defmodule MyTest do
  use ExUnit.Case, async: true

  alias Day9Part1

  test "example" do
    input = """
    R 4
    U 4
    L 3
    D 1
    R 4
    D 1
    L 5
    R 2
    """

    assert Day9Part1.run(input) === 13
  end
end

ExUnit.run()
```

## Puzzle

```elixir
defmodule Puzzle do
  alias Day9Part1

  def run do
    input = File.read!("#{__DIR__}/part1-input.txt")

    Day9Part1.run(input)
  end
end

Puzzle.run()
```
