# Day 6 Part 2

```elixir
Mix.install([])
```

## Part 1

```elixir
defmodule Day6Part2 do
  @number_of_rows 999
  @number_of_columns 999

  def run(input) do
    input
    |> String.split("\n", trim: true)
    |> Enum.reduce(initial_lights_configuration(), fn instruction, lights_configuration ->
      parsed_instruction = parse_instruction(instruction)

      update_in(
        lights_configuration,
        [
          Access.slice(parsed_instruction.from_x..parsed_instruction.to_x),
          Access.slice(parsed_instruction.from_y..parsed_instruction.to_y)
        ],
        fn light_configuration ->
          execute_instruction(light_configuration, parsed_instruction.instruction_type)
        end
      )
    end)
    |> sum_brightness()
  end

  defp initial_lights_configuration do
    for _ <- 0..@number_of_columns do
      for _ <- 0..@number_of_rows, do: 0
    end
  end

  defp parse_instruction(instruction) do
    %{
      "instruction_type" => instruction_type,
      "from_x" => from_x,
      "from_y" => from_y,
      "to_x" => to_x,
      "to_y" => to_y
    } =
      Regex.named_captures(
        ~r/(?<instruction_type>\w+) (?<from_x>\d+),(?<from_y>\d+) through (?<to_x>\d+),(?<to_y>\d+)/,
        instruction
      )

    %{
      instruction_type: instruction_type,
      from_x: String.to_integer(from_x),
      from_y: String.to_integer(from_y),
      to_x: String.to_integer(to_x),
      to_y: String.to_integer(to_y)
    }
  end

  defp sum_brightness(lights_configuration) do
    lights_configuration
    |> Enum.map(fn row_light_configurations ->
      Enum.sum(row_light_configurations)
    end)
    |> Enum.sum()
  end

  defp execute_instruction(light_configuration, "on"), do: light_configuration + 1
  defp execute_instruction(light_configuration, "off"), do: max(light_configuration - 1, 0)
  defp execute_instruction(light_configuration, "toggle"), do: light_configuration + 2
end
```

## Tests

```elixir
ExUnit.start(autorun: false)

defmodule MyTest do
  use ExUnit.Case, async: true

  alias Day6Part2

  test "examples" do
    assert Day6Part2.run("turn on 0,0 through 0,0") === 1
    assert Day6Part2.run("toggle 0,0 through 999,999") === 2_000_000
  end
end

ExUnit.run()
```

## Puzzle

```elixir
defmodule Puzzle do
  alias Day6Part2

  def run do
    input = File.read!("#{__DIR__}/part1-input.txt")

    Day6Part2.run(input)
  end
end

Puzzle.run()
```
